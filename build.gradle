plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'java'
}

group = 'auth.plugin.mc'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
}

dependencies {
    def lombok = 'org.projectlombok:lombok:1.18.32'
    compileOnly lombok
    annotationProcessor lombok

    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'

    implementation 'io.javalin:javalin:6.1.6'
    implementation 'org.slf4j:slf4j-simple:2.0.13'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
}

jar {
    setArchiveBaseName 'TelegramAuthPlugin'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

processResources {
    filesMatching('plugin.yml') {
        expand 'pluginVersion': project.version
    }
}

shadowJar {
    archiveBaseName.set('TelegramAuthPlugin')
}

task deployPlugin(type: Copy){
    from(shadowJar.archiveFile.get())
    into("\\\\WIN-E1GNAVMGMM5\\Server 1.21\\plugins")
}

shadowJar.finalizedBy deployPlugin